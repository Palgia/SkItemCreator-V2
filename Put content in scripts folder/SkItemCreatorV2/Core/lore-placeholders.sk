#
#Replace placeholders in lore of items
#
function SKICReplaceLorePlaceholders(i: item) :: item:

    set {_lore::*} to lore of {_i}

    loop {_lore::*}:

        if {_lore::%loop-index%} contains "{placeholder:":

            set {_line} to {_lore::%loop-index%}
            set {_split::*} to {_line} split at "{placeholder:"
            set {_split2::*} to {_split::2} split at "}"
            set {_finalsplit::*} to {_split2::1} split at ":"
            set {_placeholder-type} to {_finalsplit::1}
            set {_placeholder-value} to {_finalsplit::2}

            #
            #Stat placeholder
            #
            if {_placeholder-type} is "stat":
                parse if script "SkRPG/Main/!Main/SkRPG.sk" is loaded:
                    parse if script "SkRPG/Main/Addons/SkRPGItemsAddon.sk" is loaded:
                        set {_stat-amount} to int tag "SKRPGITEMS-%{_placeholder-value}%" of custom nbt of {_i}
                        set {_stat-line} to skRPGItemsAddonReturnStatLine({_placeholder-value}, {_stat-amount})
                        replace all "{placeholder:%{_placeholder-type}%:%{_placeholder-value}%}" with {_stat-line} in {_lore::%loop-index%}

            #
            #Variable placeholder
            #
            else if {_placeholder-type} is "var":
                replace all "{placeholder:%{_placeholder-type}%:%{_placeholder-value}%}" with "%{%{_placeholder-value}%}%" in {_lore::%loop-index%}

            #
            #PlaceHolderAPI
            #
            else if {_placeholder-type} is "papi":
                parse if plugin "PlaceholderAPI" is enabled:
                    replace all "{placeholder:%{_placeholder-type}%:%{_placeholder-value}%}" with SKICGetGlobalPAPIPlaceholder({_placeholder-value}) in {_lore::%loop-index%}

            #
            #Rarity
            #
            else if {_placeholder-type} is "rarity":
                replace all "{placeholder:%{_placeholder-type}%:%{_placeholder-value}%}" with SKICReturnRarityLine({_placeholder-value}) in {_lore::%loop-index%}
                set string tag "SKIC-RARITY" of custom nbt of {_i} to {_placeholder-value}

    set lore of {_i} to {_lore::*}
    return {_i}
           